;
; Copyright (c) 2025, the Jeandle-JDK Authors. All Rights Reserved.
; DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS FILE HEADER.

; This code is free software; you can redistribute it and/or modify it
; under the terms of the GNU General Public License version 2 only, as
; published by the Free Software Foundation.

; This code is distributed in the hope that it will be useful, but WITHOUT
; ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
; FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
; version 2 for more details (a copy is included in the LICENSE file that
; accompanied this code).

; You should have received a copy of the GNU General Public License version
; 2 along with this work; if not, write to the Free Software Foundation,
; Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301 USA.
;

; This file defines some LLVM functions which we call them "JavaOp". Each JavaOp represents a high-level java
; operation. These functions will be used by some passes to do Java-related optimizations. After corresponding
; optimizations, JavaOp will be inlined(lowered) by JavaOperationLower passes.

; =============================================================================================================
; Declare these runtime-related constants as global variables. The VM will define them as constants during
; Jeandle compiler initialization time.
;

; We use a null personality function for exception handlers.
@jeandle.personality = global ptr null

; Debug mode
@DEBUG_MODE = external global i1

; System word size
@WordSize = external global i64

; Byte offsets of Array<Klass*> structure fields.
@KlassArray.base_offset_in_bytes = external global i32
@KlassArray.length_offset_in_bytes = external global i32

; Byte offsets of arrayOopDesc structure fields.
@arrayOopDesc.length_offset_in_bytes = external global i32
; Per-element-type array base offset. Consumed by the LLVM-side partial escape
; analyzer (VMConstants::fromModule) so it can compute element addresses for
; virtualised arrays. The .int member predates the others.
@arrayOopDesc.base_offset_in_bytes.boolean = external global i32
@arrayOopDesc.base_offset_in_bytes.byte    = external global i32
@arrayOopDesc.base_offset_in_bytes.char    = external global i32
@arrayOopDesc.base_offset_in_bytes.short   = external global i32
@arrayOopDesc.base_offset_in_bytes.int     = external global i32
@arrayOopDesc.base_offset_in_bytes.long    = external global i32
@arrayOopDesc.base_offset_in_bytes.float   = external global i32
@arrayOopDesc.base_offset_in_bytes.double  = external global i32
@arrayOopDesc.base_offset_in_bytes.object  = external global i32
; Per-element-type array element size in bytes. Same delivery model; consumed
; by VMConstants::fromModule alongside the base offsets.
@arrayOopDesc.element_size.boolean = external global i32
@arrayOopDesc.element_size.byte    = external global i32
@arrayOopDesc.element_size.char    = external global i32
@arrayOopDesc.element_size.short   = external global i32
@arrayOopDesc.element_size.int     = external global i32
@arrayOopDesc.element_size.long    = external global i32
@arrayOopDesc.element_size.float   = external global i32
@arrayOopDesc.element_size.double  = external global i32
@arrayOopDesc.element_size.object  = external global i32

; Byte offsets for Klass structure fields.
@Klass.access_flags_offset = external global i32
@Klass.java_mirror_offset = external global i32
@Klass.layout_helper_offset = external global i32
@Klass.secondary_super_cache_offset = external global i32
@Klass.secondary_supers_offset = external global i32
@Klass.super_check_offset_offset = external global i32
@ObjArrayKlass.element_klass_offset = external global i32

; InstanceKlass initialization state used by klass_is_initialized.
@InstanceKlass.init_state_offset = external global i32
@InstanceKlass.fully_initialized = external global i8

; Byte offsets for oopDesc structure fields.
@oopDesc.klass_offset_in_bytes = external global i32
@oopDesc.mark_offset_in_bytes = external global i32
@instanceOopDesc.base_offset_in_bytes = external global i32

; Byte offsets for JavaThread structure fields.
@JavaThread.tlab_end_offset = external global i64
@JavaThread.tlab_top_offset = external global i64

; Byte offsets for markWord structure fields.
@markWord.prototype_value = external global i64

; Global vm options
@VMOptions.UseTLAB = external global i1
@VMOptions.ZeroTLAB = external global i1
; Heap layout switches consumed by VMConstants::fromModule on the LLVM side.
@VMOptions.UseCompressedClassPointers = external global i1
@VMOptions.UseCompressedOops = external global i1

; Arraycopy optimization thresholds supplied by HotSpot at startup.
@VMOptions.ArrayOperationPartialInlineSize = external global i32
@VMOptions.ArrayCopyLoadStoreMaxElem = external global i32

; Byte offsets for java.lang.ref.Reference instance fields.
@java_lang_ref_Reference.referent_offset = external global i32

; Byte offset of the represented Klass* in java.lang.Class.
@java_lang_Class.klass_offset = external global i32

; Byte offset of the cached array klass in java.lang.Class (injected field).
; Stores the array Klass* for this component type once the array type has been loaded.
; Zero/null means the array klass has not yet been resolved.
@java_lang_Class.array_klass_offset = external global i32

; Byte offsets for BasicLock structure fields.
@BasicLock.displaced_header_offset_in_bytes = external global i32

; Byte offsets for JavaThread structure fields.
@JavaThread.held_monitor_count_offset = external global i32
@JavaThread.lock_stack_end = external global i32
@JavaThread.lock_stack_top_offset = external global i32

; Byte offsets for ObjectMonitor structure fields.
@ObjectMonitor.EntryList_offset_no_monitor_value = external global i32
@ObjectMonitor.cxq_offset_no_monitor_value = external global i32
@ObjectMonitor.owner_offset_no_monitor_value = external global i32
@ObjectMonitor.recursions_offset_no_monitor_value = external global i32
@ObjectMonitor.succ_offset_no_monitor_value = external global i32
@ObjectMonitor.ANONYMOUS_OWNER = external global i64

; Static constants in markWord
@markWord.clear_lock_mask = external global i64
@markWord.monitor_value = external global i64
@markWord.unlocked_value = external global i64
@markWord.unused_mark_value = external global i64

; Global definitions
@JVM_ACC_IS_VALUE_BASED_CLASS = external global i32
@JVM_ACC_HAS_FINALIZER = external global i32
@oopSize = external global i32
@check_recursive_mask_value = external global i64

; Byte offsets for G1ThreadLocalData structure fields.
@G1ThreadLocalData.satb_mark_queue_active_offset = external global i32
@G1ThreadLocalData.satb_mark_queue_index_offset = external global i32
@G1ThreadLocalData.satb_mark_queue_buffer_offset = external global i32
@G1ThreadLocalData.dirty_card_queue_index_offset = external global i32
@G1ThreadLocalData.dirty_card_queue_buffer_offset = external global i32

; Card table constants
@CardTable.card_shift = external global i64
@ci_card_table_address = external global i64
@HeapRegion.LogOfHRGrainBytes = external global i64
@G1CardTable.g1_young_card_val = external global i8
@G1CardTable.dirty_card_val = external global i8

; Address for G1BarrierSetRuntime functions.
@G1BarrierSetRuntime.write_ref_field_pre_entry = external global i64
@G1BarrierSetRuntime.write_ref_field_post_entry = external global i64

; Address for the monitorexit slow-path routine (SharedRuntime::complete_monitor_unlocking_C).
; It is a direct routine with reachable=false, so the monitorexit slow paths call it via this
; baked absolute address (load + inttoptr + indirect call) rather than a PC-relative branch.
@SharedRuntime.complete_monitor_unlocking_C = external global i64

; Keep use to lately-used java operations, until it is lowered.
@llvm.used = appending addrspace(1) global [7 x ptr] [
  ptr @jeandle.card_table_barrier,
  ptr @jeandle.g1_pre_barrier,
  ptr @jeandle.g1_post_barrier,
  ptr @jeandle.pre_barrier,
  ptr @jeandle.post_barrier,
  ptr @jeandle.encode_heap_oop,
  ptr @jeandle.decode_heap_oop
], section "llvm.metadata"

declare hotspotcc ptr addrspace(0) @jeandle.decode_klass(i32)
declare hotspotcc i32 @jeandle.encode_klass(ptr addrspace(0))

declare hotspotcc ptr addrspace(1) @jeandle.decode_heap_oop(ptr addrspace(3))
declare hotspotcc ptr addrspace(3) @jeandle.encode_heap_oop(ptr addrspace(1))

; Load klass pointer from oop
; lower-phase=2: survive JavaOperationLower(0) so the call reaches PEA, which
; folds it via foldLoadKlass (a virtual object's klass is a compile-time
; constant). At lower-phase=0 the body is expanded before PEA, exposing a raw
; load of the object's klass header; resolveAccess returns nullopt for header
; offsets and processLoad marks the object ineligible — defeating
; virtualization for any virtual receiver whose klass is inspected (e.g. the
; load_klass inside instanceof's expansion on a virtual receiver). Non-virtual
; receivers are unaffected: the call survives to JavaOperationLower(2) and the
; same body is inlined there.
define hotspotcc ptr addrspace(0) @jeandle.load_klass(ptr addrspace(1) nocapture %oop) noinline "lower-phase"="2" #0 {
  %klass_offset = load i32, ptr @oopDesc.klass_offset_in_bytes
  %klass_addr = getelementptr inbounds i8, ptr addrspace(1) %oop, i32 %klass_offset

  %use_compressed = load i1, ptr @VMOptions.UseCompressedClassPointers
  br i1 %use_compressed, label %compressed, label %uncompressed

compressed:
  %narrow = load atomic i32, ptr addrspace(1) %klass_addr unordered, align 4
  %decoded = call hotspotcc ptr addrspace(0) @jeandle.decode_klass(i32 %narrow)
  ret ptr addrspace(0) %decoded

uncompressed:
  %wide = load atomic ptr addrspace(0), ptr addrspace(1) %klass_addr unordered, align 8
  ret ptr addrspace(0) %wide
}

; Test whether a previously loaded dynamic Klass is exactly the expected Klass.
; Profile devirtualization shares one phase-1 load across all exact checks for
; a receiver. JavaType traces actual_klass back to that load for path-sensitive
; type propagation before JavaOperationLower(1) expands both operations.
define hotspotcc i1 @jeandle.check_exact_klass(ptr addrspace(0) nocapture %expected_klass, ptr addrspace(0) nocapture %actual_klass) noinline "lower-phase"="1" #0 {
  %is_exact = icmp eq ptr addrspace(0) %actual_klass, %expected_klass
  ret i1 %is_exact
}

; Load the reference Klass represented by a java.lang.Class mirror. Primitive
; mirrors contain a null Klass*. Keeping this as a phase-2 JavaOp lets
; ConstantFieldFolding answer the query from a constant mirror before exposing
; the VM-injected field load.
define hotspotcc ptr addrspace(0) @jeandle.load_mirror_klass(ptr addrspace(1) nocapture readonly %mirror) noinline "lower-phase"="2" #0 {
entry:
  %klass_offset = load i32, ptr @java_lang_Class.klass_offset
  %klass_addr = getelementptr inbounds i8, ptr addrspace(1) %mirror, i32 %klass_offset
  %klass = load atomic ptr addrspace(0), ptr addrspace(1) %klass_addr unordered, align 8
  ret ptr addrspace(0) %klass
}

; Load Klass::layout_helper from a Klass pointer. Keeping this as a phase-2
; JavaOp lets ConstantFieldFolding answer the query while the Klass is still a
; compile-time constant; unresolved queries lower to the ordinary VM load.
define hotspotcc i32 @jeandle.layout_helper(ptr addrspace(0) nocapture readonly %klass) noinline "lower-phase"="2" #0 {
entry:
  %layout_helper_offset = load i32, ptr @Klass.layout_helper_offset
  %layout_helper_addr = getelementptr inbounds i8, ptr addrspace(0) %klass, i32 %layout_helper_offset
  %layout_helper = load atomic i32, ptr addrspace(0) %layout_helper_addr unordered, align 4, !invariant.load !{}
  ret i32 %layout_helper
}

; Query a Klass' current initialization state. Keeping this opaque through
; phase 0 lets ConstantFieldFolding replace the query with true for a constant
; Klass already known by the VM to be initialized. Otherwise phase 2 lowers it
; to the runtime state load, preserving later class initialization.
define hotspotcc i1 @jeandle.klass_is_initialized(ptr addrspace(0) nocapture readonly %klass) noinline "lower-phase"="2" #0 {
entry:
  %init_state_offset = load i32, ptr @InstanceKlass.init_state_offset
  %init_state_addr = getelementptr inbounds i8, ptr addrspace(0) %klass, i32 %init_state_offset
  %init_state = load volatile i8, ptr addrspace(0) %init_state_addr, align 1
  %fully_initialized = load i8, ptr @InstanceKlass.fully_initialized
  %is_initialized = icmp eq i8 %init_state, %fully_initialized
  ret i1 %is_initialized
}

; Load the element Klass from an ObjArrayKlass.
define hotspotcc ptr addrspace(0) @jeandle.load_array_element_klass(ptr addrspace(0) nocapture %array_klass) noinline "lower-phase"="2" #0 {
  %element_klass_offset = load i32, ptr @ObjArrayKlass.element_klass_offset
  %element_klass_addr = getelementptr inbounds i8, ptr addrspace(0) %array_klass, i32 %element_klass_offset
  %element_klass = load atomic ptr addrspace(0), ptr addrspace(0) %element_klass_addr unordered, align 8
  ret ptr addrspace(0) %element_klass
}

; This is the slow path for subtype checking when the fast path fails.
define hotspotcc i1 @jeandle.check_klass_subtype_slow_path(ptr addrspace(0) nocapture %sub_klass, ptr addrspace(0) nocapture %super_klass) "lower-phase"="0" #0 {
entry:
  ; Load secondary_supers array and secondary_super_cache.
  %secondary_supers_offset = load i32, ptr @Klass.secondary_supers_offset
  %secondary_supers_addr = getelementptr inbounds i8, ptr addrspace(0) %sub_klass, i32 %secondary_supers_offset
  %secondary_supers = load atomic ptr addrspace(0), ptr addrspace(0) %secondary_supers_addr unordered, align 8

  ; Load length and base address of secondary_supers array.
  %length_offset = load i32, ptr @KlassArray.length_offset_in_bytes
  %length_addr = getelementptr inbounds i8, ptr addrspace(0) %secondary_supers, i32 %length_offset
  %length = load atomic i32, ptr addrspace(0) %length_addr unordered, align 4
  %base_offset = load i32, ptr @KlassArray.base_offset_in_bytes
  %base_addr = getelementptr inbounds i8, ptr addrspace(0) %secondary_supers, i32 %base_offset

  br label %scan_loop

scan_loop:
  ; Scan the secondary_supers until the super_klass is found, if not found, then return false.
  %index = phi i32 [0, %entry], [%next_index, %continue_loop]
  %current_ptr = phi ptr [%base_addr, %entry], [%next_ptr, %continue_loop]

  ; Check loop end
  %scan_done = icmp eq i32 %index, %length
  br i1 %scan_done, label %return_false, label %loop_body

loop_body:
  %current_klass = load atomic ptr addrspace(0), ptr addrspace(0) %current_ptr unordered, align 8
  %is_match = icmp eq ptr addrspace(0) %super_klass, %current_klass
  br i1 %is_match, label %return_true, label %continue_loop

continue_loop:
  %next_index = add i32 %index, 1
  %next_ptr = getelementptr ptr, ptr addrspace(0) %base_addr, i32 %next_index
  br label %scan_loop

return_true:
  ; Success, cache the super klass we found.
  %secondary_super_cache_offset = load i32, ptr @Klass.secondary_super_cache_offset
  %secondary_super_cache_addr = getelementptr inbounds i8, ptr addrspace(0) %sub_klass, i32 %secondary_super_cache_offset
  store atomic ptr addrspace(0) %super_klass, ptr addrspace(0) %secondary_super_cache_addr unordered, align 8

  ret i1 true

return_false:
  ret i1 false
}

; Check if the sub_klass extends from the super_klass using both primary and secondary supers.
; Fast path: checks primary super chain.
; Slow path: scans secondary supers array if needed.
define hotspotcc i1 @jeandle.check_klass_subtype(ptr addrspace(0) nocapture %sub_klass, ptr addrspace(0) nocapture %super_klass) "lower-phase"="2" #0 {
entry:
  %is_same_klass = icmp eq ptr addrspace(0) %sub_klass, %super_klass
  br i1 %is_same_klass, label %return_true, label %check_primary_supers

check_primary_supers:
  ; Load super_check_offset_offset
  %super_check_offset_offset = load i32, ptr @Klass.super_check_offset_offset
  %super_check_offset_addr = getelementptr inbounds i8, ptr addrspace(0) %super_klass, i32 %super_check_offset_offset
  %super_check_offset = load atomic i32, ptr addrspace(0) %super_check_offset_addr unordered, align 4

  ; Load super_check klass from _primary_supers of the sub_klass.
  %super_check_addr = getelementptr inbounds i8, ptr addrspace(0) %sub_klass, i32 %super_check_offset
  %super_check = load atomic ptr addrspace(0), ptr addrspace(0) %super_check_addr unordered, align 8

  %is_super_match = icmp eq ptr %super_klass, %super_check
  br i1 %is_super_match, label %return_true, label %check_secondary_supers

check_secondary_supers:
  ; Check if there are secondary supers.
  %secondary_super_cache_offset = load i32, ptr @Klass.secondary_super_cache_offset
  %has_secondary = icmp eq i32 %super_check_offset, %secondary_super_cache_offset
  br i1 %has_secondary, label %slow_path, label %return_false

slow_path:
  %is_subtype_slow = call hotspotcc i1 @jeandle.check_klass_subtype_slow_path(ptr addrspace(0) %sub_klass, ptr addrspace(0) %super_klass)
  br i1 %is_subtype_slow, label %return_true, label %return_false

return_true:
  ret i1 true
return_false:
  ret i1 false
}

define hotspotcc i1 @jeandle.check_instanceof(ptr addrspace(0) nocapture %super_klass, ptr addrspace(1) nocapture nonnull %oop) noinline "lower-phase"="2" #0 {
entry:
  %sub_klass = call hotspotcc ptr addrspace(0) @jeandle.load_klass(ptr addrspace(1) nonnull %oop)
  %is_subtype = call hotspotcc i1 @jeandle.check_klass_subtype(ptr addrspace(0) %sub_klass, ptr addrspace(0) %super_klass)
  ret i1 %is_subtype
}

; Implementation of Java instanceof operation.
define hotspotcc i32 @jeandle.instanceof(ptr addrspace(0) nocapture %super_klass, ptr addrspace(1) nocapture %oop) noinline "lower-phase"="0" #0 {
entry:
  %is_null = icmp eq ptr addrspace(1) %oop, null
  br i1 %is_null, label %return_false, label %check_subtype

return_false:
  ret i32 0

check_subtype:
  %is_subtype = call hotspotcc i1 @jeandle.check_instanceof(ptr addrspace(0) %super_klass, ptr addrspace(1) nocapture nonnull %oop)
  %is_subtype_ext = zext i1 %is_subtype to i32
  ret i32 %is_subtype_ext
}

declare hotspotcc ptr addrspace(1) @new_instance(ptr, ptr)

; Implementation of Java new object
; TODO: Support prefetch instructions for next allocations.
define private hotspotcc nonnull ptr addrspace(1) @jeandle.new_instance(ptr %klass, i32 %size_in_bytes, i1 %initial_slow_test) noinline "lower-phase"="2" "jeandle.not-guaranteed-safepoint" {
entry:
  ; The caller may already know that this allocation requires the runtime.
  ; Share that path with a TLAB exhaustion instead of creating another call.
  br i1 %initial_slow_test, label %alloc_slow_path, label %check_tlab

check_tlab:
  %use_tlab = load i1, ptr @VMOptions.UseTLAB
  br i1 %use_tlab, label %test_tlab, label %alloc_slow_path

test_tlab:
  %tlab_top_offset = load i64, ptr @JavaThread.tlab_top_offset
  %tlab_end_offset = load i64, ptr @JavaThread.tlab_end_offset

  %tlab_top_ptr = inttoptr i64 %tlab_top_offset to ptr addrspace(2)
  %tlab_end_ptr = inttoptr i64 %tlab_end_offset to ptr addrspace(2)

  %tlab_old_top = load ptr addrspace(1), ptr addrspace(2) %tlab_top_ptr, align 8
  %tlab_end = load ptr addrspace(1), ptr addrspace(2) %tlab_end_ptr, align 8

  %tlab_new_top = getelementptr i8, ptr addrspace(1) %tlab_old_top, i32 %size_in_bytes
  %if_tlab_full = icmp uge ptr addrspace(1) %tlab_new_top, %tlab_end
  br i1 %if_tlab_full, label %alloc_slow_path, label %alloc_fast_path

alloc_slow_path:
  %current_thread = call hotspotcc ptr @jeandle.current_thread()
  %slow_alloc_obj = call hotspotcc ptr addrspace(1) @new_instance(ptr %klass, ptr %current_thread) [ "deopt"() ]
  br label %return_block

alloc_fast_path:
  store ptr addrspace(1) %tlab_new_top, ptr addrspace(2) %tlab_top_ptr, align 8
  %mark_word_offset = load i32, ptr @oopDesc.mark_offset_in_bytes
  %mark_word_addr = getelementptr i8, ptr addrspace(1) %tlab_old_top, i32 %mark_word_offset

  %klass_offset = load i32, ptr @oopDesc.klass_offset_in_bytes
  %klass_addr = getelementptr i8, ptr addrspace(1) %tlab_old_top, i32 %klass_offset

  %prototype_value = load i64, ptr @markWord.prototype_value

  store atomic i64 %prototype_value, ptr addrspace(1) %mark_word_addr unordered, align 8

  %use_compressed_klass = load i1, ptr @VMOptions.UseCompressedClassPointers
  br i1 %use_compressed_klass, label %store_narrow_klass, label %store_wide_klass

store_narrow_klass:
  %narrow_klass = call hotspotcc i32 @jeandle.encode_klass(ptr %klass)
  store atomic i32 %narrow_klass, ptr addrspace(1) %klass_addr unordered, align 4
  br label %post_klass_store

store_wide_klass:
  store atomic ptr %klass, ptr addrspace(1) %klass_addr unordered, align 8
  br label %post_klass_store

post_klass_store:
  %zero_tlab = load i1, ptr @VMOptions.ZeroTLAB
  %skip_clear = and i1 %use_tlab, %zero_tlab
  br i1 %skip_clear, label %initialization_membar, label %clear_memory

clear_memory:
  %base_offset = load i32, ptr @instanceOopDesc.base_offset_in_bytes
  %base_addr = getelementptr i8, ptr addrspace(1) %tlab_old_top, i32 %base_offset
  %payload_size = sub i32 %size_in_bytes, %base_offset
  call void @llvm.memset.p1.i32(ptr addrspace(1) align 8 %base_addr, i8 0, i32 %payload_size, i1 false)
  br label %initialization_membar

initialization_membar:
  ; TODO: The current approach uses atomic stores for mark word and klass initialization,
  ; and relies on this fence release as a temporary solution to ensure publication semantics.
  ; The goal is to replace the atomic stores with plain stores and implement a custom lightweight
  ; membar instead of this fence.
  fence release
  br label %return_block

return_block:
  %obj = phi ptr addrspace(1) [ %tlab_old_top, %initialization_membar ], [ %slow_alloc_obj, %alloc_slow_path ]
  ret ptr addrspace(1) %obj
}

; Implementation of Java arraylength operation.
; lower-phase=1: survive JavaOperationLower(0) so the call reaches PEA, which
; folds it via foldArrayLength (a virtual array's length is a compile-time
; constant). The frontend emits this op both for the arraylength bytecode and
; for bounds checks, so every virtual array's element access benefits.
; processLoad's length-offset fold remains as the fallback for any raw
; length-header load (e.g. produced by hand-written IR). Non-virtual receivers
; are unaffected: the call survives to JavaOperationLower(1) and the same body
; is inlined there.
define hotspotcc i32 @jeandle.arraylength(ptr addrspace(1) nocapture readonly %array_oop) noinline "lower-phase"="1" #0 {
entry:
  %length_offset = load i32, ptr @arrayOopDesc.length_offset_in_bytes
  %length_addr = getelementptr inbounds i8, ptr addrspace(1) %array_oop, i32 %length_offset
  ; A Java array's length is defined, non-negative and immutable during the
  ; array's lifetime. Expose those facts to SCEV, LICM and LoopPredication.
  %length = load atomic i32, ptr addrspace(1) %length_addr unordered, align 4,
      !range !{i32 0, i32 2147483647}, !invariant.load !{}, !noundef !{}
  ret i32 %length
}

declare hotspotcc ptr @jeandle.current_thread()
declare hotspotcc ptr addrspace(1) @new_array(ptr, i32, ptr)
declare hotspotcc void @SharedRuntime_register_finalizer(ptr, ptr addrspace(1))

; ArrayCopyNode-like pseudo operation. Keep it opaque through phase 0 so
declare hotspotcc void @jeandle.arraycopy(
    ptr addrspace(1), i32, ptr addrspace(1), i32, i32,
    ptr addrspace(0), ptr addrspace(0), i32, i32
)

; Slow-path runtime routines for monitor JavaOps. The LOCKING routine is an
; indirect routine called via a JIT stub (hotspotcc); declared here and
; resolved at link time via a routine-call reloc to the stub. The UNLOCKING
; routine is a direct leaf (reachable=false) whose far C++ address cannot be
; reached by a PC-relative jump, so it is NOT declared here; the monitorexit
; slow paths call it via a baked absolute-address global
; (@SharedRuntime.complete_monitor_unlocking_C) defined in
; jeandleRuntimeDefinedJavaOps.cpp — same pattern as the G1 barrier globals.
declare hotspotcc void @SharedRuntime_complete_monitor_locking_C(ptr addrspace(1), ptr addrspace(0), ptr)

; IR-level runtime target that RewriteStatepointsForGC lowers each
; llvm.experimental.deoptimize call onto (RewriteStatepointsForGC.cpp). Declared
; here rather than synthesized per-method so every compilation module starts with
; it. The `hotspotcc` convention (= CallingConv::Hotspot_JIT) MUST match the
; uncommon_trap_blob entry resolved at JIT link time (jeandleRuntimeRoutine.hpp);
; do not simplify to the default CC -- RS4GC would otherwise synthesize a
; default-CC declaration and the lowered call would mismatch the runtime entry.
declare hotspotcc void @__llvm_deoptimize(i32)

; Arraycopy stub entry points resolved by jeandleRuntimeRoutine.hpp.
declare i32 @StubRoutines_generic_arraycopy(ptr addrspace(1), i32, ptr addrspace(1), i32, i32)
declare hotspotcc void @SharedRuntime_slow_arraycopy_C(ptr addrspace(1), i32, ptr addrspace(1), i32, i32, ptr)
declare void @StubRoutines_jbyte_arraycopy(ptr addrspace(1), ptr addrspace(1), i64)
declare void @StubRoutines_arrayof_jbyte_arraycopy(ptr addrspace(1), ptr addrspace(1), i64)
declare void @StubRoutines_jbyte_disjoint_arraycopy(ptr addrspace(1), ptr addrspace(1), i64)
declare void @StubRoutines_arrayof_jbyte_disjoint_arraycopy(ptr addrspace(1), ptr addrspace(1), i64)
declare void @StubRoutines_jshort_arraycopy(ptr addrspace(1), ptr addrspace(1), i64)
declare void @StubRoutines_arrayof_jshort_arraycopy(ptr addrspace(1), ptr addrspace(1), i64)
declare void @StubRoutines_jshort_disjoint_arraycopy(ptr addrspace(1), ptr addrspace(1), i64)
declare void @StubRoutines_arrayof_jshort_disjoint_arraycopy(ptr addrspace(1), ptr addrspace(1), i64)
declare void @StubRoutines_jint_arraycopy(ptr addrspace(1), ptr addrspace(1), i64)
declare void @StubRoutines_arrayof_jint_arraycopy(ptr addrspace(1), ptr addrspace(1), i64)
declare void @StubRoutines_jint_disjoint_arraycopy(ptr addrspace(1), ptr addrspace(1), i64)
declare void @StubRoutines_arrayof_jint_disjoint_arraycopy(ptr addrspace(1), ptr addrspace(1), i64)
declare void @StubRoutines_jlong_arraycopy(ptr addrspace(1), ptr addrspace(1), i64)
declare void @StubRoutines_arrayof_jlong_arraycopy(ptr addrspace(1), ptr addrspace(1), i64)
declare void @StubRoutines_jlong_disjoint_arraycopy(ptr addrspace(1), ptr addrspace(1), i64)
declare void @StubRoutines_arrayof_jlong_disjoint_arraycopy(ptr addrspace(1), ptr addrspace(1), i64)
declare void @StubRoutines_oop_arraycopy(ptr addrspace(1), ptr addrspace(1), i64)
declare void @StubRoutines_arrayof_oop_arraycopy(ptr addrspace(1), ptr addrspace(1), i64)
declare void @StubRoutines_oop_disjoint_arraycopy(ptr addrspace(1), ptr addrspace(1), i64)
declare void @StubRoutines_arrayof_oop_disjoint_arraycopy(ptr addrspace(1), ptr addrspace(1), i64)
declare i32 @StubRoutines_checkcast_arraycopy(ptr addrspace(1), ptr addrspace(1), i64, i64, ptr addrspace(0))


; Unified array allocation JavaOp.  Both bytecode (newarray/anewarray) and intrinsic
; (_newArray / Array.newInstance) paths call this function.
; LLVM passes identify array allocation by matching on this function name.
define private hotspotcc nonnull ptr addrspace(1) @jeandle.new_array(ptr %array_klass, i32 %length, i32 %size_in_bytes, i32 %base_offset, i32 %length_limit) noinline "lower-phase"="2" "jeandle.not-guaranteed-safepoint" {
entry:
  ; Fast-path length cap. %length_limit is a size-derived bound (FastAllocateSizeLimit, scaled
  ; by element size on the caller side) chosen so %size_in_bytes cannot overflow i32 -- it is
  ; NOT arrayOopDesc::max_array_length(), which is ~max_jint on LP64 and would let a large
  ; length wrap the size computation. The unsigned compare also routes a negative %length
  ; (e.g. -1) to the slow path, where the runtime raises NegativeArraySizeException.
  %too_long = icmp ugt i32 %length, %length_limit
  br i1 %too_long, label %array_slow_path, label %check_tlab

check_tlab:
  %use_tlab = load i1, ptr @VMOptions.UseTLAB
  br i1 %use_tlab, label %test_tlab, label %array_slow_path

test_tlab:
  %tlab_top_offset = load i64, ptr @JavaThread.tlab_top_offset
  %tlab_end_offset = load i64, ptr @JavaThread.tlab_end_offset

  %tlab_top_ptr = inttoptr i64 %tlab_top_offset to ptr addrspace(2)
  %tlab_end_ptr = inttoptr i64 %tlab_end_offset to ptr addrspace(2)

  %tlab_old_top = load ptr addrspace(1), ptr addrspace(2) %tlab_top_ptr, align 8
  %tlab_end = load ptr addrspace(1), ptr addrspace(2) %tlab_end_ptr, align 8

  %tlab_new_top = getelementptr i8, ptr addrspace(1) %tlab_old_top, i32 %size_in_bytes
  %if_tlab_full = icmp uge ptr addrspace(1) %tlab_new_top, %tlab_end
  br i1 %if_tlab_full, label %array_slow_path, label %array_fast_path

array_slow_path:
  %current_thread = call hotspotcc ptr @jeandle.current_thread()
  %slow_array_oop = call hotspotcc ptr addrspace(1) @new_array(ptr %array_klass, i32 %length, ptr %current_thread) [ "deopt"() ]
  br label %array_return

array_fast_path:
  store ptr addrspace(1) %tlab_new_top, ptr addrspace(2) %tlab_top_ptr, align 8

  ; Header: mark word, klass pointer, length. No inter-field barriers; the
  ; trailing release fence publishes the whole object as a unit.
  %mark_word_offset = load i32, ptr @oopDesc.mark_offset_in_bytes
  %mark_word_addr = getelementptr i8, ptr addrspace(1) %tlab_old_top, i32 %mark_word_offset

  %klass_offset = load i32, ptr @oopDesc.klass_offset_in_bytes
  %klass_addr = getelementptr i8, ptr addrspace(1) %tlab_old_top, i32 %klass_offset

  %length_offset = load i32, ptr @arrayOopDesc.length_offset_in_bytes
  %length_addr = getelementptr i8, ptr addrspace(1) %tlab_old_top, i32 %length_offset

  %prototype_value = load i64, ptr @markWord.prototype_value

  store atomic i64 %prototype_value, ptr addrspace(1) %mark_word_addr unordered, align 8
  %array_use_compressed_klass = load i1, ptr @VMOptions.UseCompressedClassPointers
  br i1 %array_use_compressed_klass, label %array_store_narrow_klass, label %array_store_wide_klass

array_store_narrow_klass:
  %array_narrow_klass = call hotspotcc i32 @jeandle.encode_klass(ptr %array_klass)
  store atomic i32 %array_narrow_klass, ptr addrspace(1) %klass_addr unordered, align 4
  br label %array_post_klass_store

array_store_wide_klass:
  store atomic ptr %array_klass, ptr addrspace(1) %klass_addr unordered, align 8
  br label %array_post_klass_store

array_post_klass_store:
  store atomic i32 %length, ptr addrspace(1) %length_addr unordered, align 4

  %zero_tlab = load i1, ptr @VMOptions.ZeroTLAB
  %skip_clear = and i1 %use_tlab, %zero_tlab
  br i1 %skip_clear, label %array_init_membar, label %array_clear_memory

array_clear_memory:
  ; Explicit 8-byte-stride zero loop. We do NOT use @llvm.memset because LLVM's
  ; default lowering produces poor code for both target backends we care about:
  ;   - AArch64 (without MOPS): every memset, constant size or not, falls back
  ;     to a per-byte strb loop -- the target hook in AArch64SelectionDAGInfo
  ;     just `return SDValue()` and lets the generic SelectionDAG byte expansion
  ;     take over. That's ~10x slower than the str xzr / stp xzr,xzr sequence
  ;     HotSpot C1 emits.
  ;   - x86: constant-size memset gets the rep stosq fast path, but variable
  ;     size is bailed out by X86SelectionDAGInfo (`if (!ConstantSize) return
  ;     SDValue();`) and ends up in the same generic per-byte loop. So variable-
  ;     length arrays (the common `new T[N]` case in real workloads) regress on
  ;     x86 too, not just AArch64.
  ;
  ; The stores must be `volatile` -- otherwise LLVM's LoopIdiomRecognize pass
  ; folds an idiomatic "for(i=0;i<n;i++) p[i]=0" loop right back into
  ; @llvm.memset, which round-trips through the same broken lowering. Volatile
  ; stores are exempt from idiom recognition, so they survive into codegen as
  ; plain str xzr (AArch64) / `mov qword ptr [...], 0` (x86) 8-byte stores.
  ; LoopFullUnroll still applies for constant trip counts, so `new T[N]` with
  ; N a compile-time constant becomes a fully unrolled sequence.
  ;
  ; TODO: For payloads larger than ~256 bytes (HotSpot AArch64
  ; BlockZeroingLowLimit default), HotSpot's MacroAssembler::zero_words calls
  ; the zero_blocks stub, which uses `dc zva` (64-byte cache-line zero). The
  ; single-str loop here is ~4x slower than `dc zva` for large arrays; emitting
  ; platform-specific inline asm or an LLVM target intrinsic for that tier is a
  ; follow-up. The current loop already closes the biggest gap (per-byte ->
  ; per-word).
  ;
  ; size_in_bytes is aligned to MinObjAlignmentInBytes (8) on the caller side,
  ; and base_offset is always a multiple of 8, so payload_size is exactly
  ; divisible by 8.
  %base_addr = getelementptr i8, ptr addrspace(1) %tlab_old_top, i32 %base_offset
  %payload_size = sub i32 %size_in_bytes, %base_offset
  %payload_words = lshr exact i32 %payload_size, 3
  %has_payload = icmp ne i32 %payload_words, 0
  br i1 %has_payload, label %array_zero_loop, label %array_init_membar

array_zero_loop:
  %zi = phi i32 [ 0, %array_clear_memory ], [ %zi_next, %array_zero_loop ]
  %zaddr = getelementptr i64, ptr addrspace(1) %base_addr, i32 %zi
  store volatile i64 0, ptr addrspace(1) %zaddr, align 8
  %zi_next = add nuw nsw i32 %zi, 1
  %zero_done = icmp eq i32 %zi_next, %payload_words
  br i1 %zero_done, label %array_init_membar, label %array_zero_loop

array_init_membar:
  ; TODO: Same plan as new_instance -- replace atomic stores + fence release with plain
  ; stores plus a lightweight publish barrier once the supporting LLVM pass lands.
  fence release
  br label %array_return

array_return:
  %array_oop = phi ptr addrspace(1) [ %tlab_old_top, %array_init_membar ], [ %slow_array_oop, %array_slow_path ]
  ret ptr addrspace(1) %array_oop
}

; Declaration of Java card table barrier.
; Phase 9 barrier JavaOps are lowered after RS4GC. They materialize raw
; addresses derived from oops, such as card-table addresses, which RS4GC does
; not track. Keeping them opaque until phase 9 prevents O3 from reusing those
; raw derived addresses across safepoints.
declare hotspotcc void @jeandle.card_table_barrier(ptr addrspace(1) %addr)

define private hotspotcc void @jeandle.g1_satb_enqueue(ptr addrspace(1) %pre_val) "lower-phase"="2" #0 {
entry:
  %index_offset = load i32, ptr @G1ThreadLocalData.satb_mark_queue_index_offset
  %buffer_offset = load i32, ptr @G1ThreadLocalData.satb_mark_queue_buffer_offset
  %index_adr = inttoptr i32 %index_offset to ptr addrspace(2)
  %buffer_adr = inttoptr i32 %buffer_offset to ptr addrspace(2)
  %index = load i64, ptr addrspace(2) %index_adr
  %buffer = load ptr, ptr addrspace(2) %buffer_adr
  %is_zero = icmp eq i64 %index, 0
  br i1 %is_zero, label %buffer_is_full, label %store_in_buffer

buffer_is_full:
  %callee_addr = load i64, ptr @G1BarrierSetRuntime.write_ref_field_pre_entry
  %callee = inttoptr i64 %callee_addr to ptr
  %current_thread = call hotspotcc ptr @jeandle.current_thread()
  call void %callee(ptr addrspace(1) %pre_val, ptr %current_thread) #0
  br label %done

store_in_buffer:
  %wordsize = load i64, ptr @WordSize
  %next_index = sub i64 %index, %wordsize
  %log_addr = getelementptr inbounds i8, ptr %buffer, i64 %next_index
  store atomic ptr addrspace(1) %pre_val, ptr %log_addr unordered, align 8
  store atomic i64 %next_index, ptr addrspace(2) %index_adr unordered, align 8
  br label %done

done:
  ret void
}

; Implementation of Java g1 pre barrier.
define private hotspotcc void @jeandle.g1_pre_barrier(ptr addrspace(1) %addr) noinline "lower-phase"="9" #0 {
entry:
  %marking_offset = load i32, ptr @G1ThreadLocalData.satb_mark_queue_active_offset
  %marking_adr = inttoptr i32 %marking_offset to ptr addrspace(2)
  %marking = load i8, ptr addrspace(2) %marking_adr
  %is_not_marking = icmp eq i8 %marking, 0
  br i1 %is_not_marking, label %done, label %load_pre_value

done:
  ret void

load_pre_value:
  %use_compressed = load i1, ptr @VMOptions.UseCompressedOops
  br i1 %use_compressed, label %compressed, label %uncompressed

compressed:
  %narrow_val = load atomic ptr addrspace(3), ptr addrspace(1) %addr unordered, align 4
  %narrow_is_null = icmp eq ptr addrspace(3) %narrow_val, null
  br i1 %narrow_is_null, label %done, label %decode_narrow

decode_narrow:
  %pre_val_c = addrspacecast ptr addrspace(3) %narrow_val to ptr addrspace(1)
  br label %enqueue
  
uncompressed:
  %pre_val_u = load atomic ptr addrspace(1), ptr addrspace(1) %addr unordered, align 8
  %wide_is_null = icmp eq ptr addrspace(1) %pre_val_u, null
  br i1 %wide_is_null, label %done, label %enqueue

enqueue:
  %pre_val = phi ptr addrspace(1) [ %pre_val_c, %decode_narrow ], [ %pre_val_u, %uncompressed ]
  call hotspotcc void @jeandle.g1_satb_enqueue(ptr addrspace(1) %pre_val)
  ret void
}

; Implementation of Java g1 pre barrier loaded.
define private hotspotcc void @jeandle.g1_pre_barrier_loaded(ptr addrspace(1) %pre_val) noinline "lower-phase"="9" #0 {
entry:
  %marking_offset = load i32, ptr @G1ThreadLocalData.satb_mark_queue_active_offset
  %marking_adr = inttoptr i32 %marking_offset to ptr addrspace(2)
  %marking = load i8, ptr addrspace(2) %marking_adr
  %is_not_marking = icmp eq i8 %marking, 0
  br i1 %is_not_marking, label %done, label %check_null

done:
  ret void

check_null:
  %is_null = icmp eq ptr addrspace(1) %pre_val, null
  br i1 %is_null, label %done, label %enqueue

enqueue:
  call hotspotcc void @jeandle.g1_satb_enqueue(ptr addrspace(1) %pre_val)
  ret void
}

; Implementation of Java g1 post barrier.
define private hotspotcc void @jeandle.g1_post_barrier(ptr addrspace(1) %addr, ptr addrspace(1) captures(none) %oop) noinline "lower-phase"="9" #0 {
entry:
  %index_offset = load i32, ptr @G1ThreadLocalData.dirty_card_queue_index_offset
  %buffer_offset = load i32, ptr @G1ThreadLocalData.dirty_card_queue_buffer_offset
  %index_adr = inttoptr i32 %index_offset to ptr addrspace(2)
  %buffer_adr = inttoptr i32 %buffer_offset to ptr addrspace(2)
  %addr.int = ptrtoint ptr addrspace(1) %addr to i64
  %card_shift = load i64, ptr @CardTable.card_shift
  %card_offset = lshr i64 %addr.int, %card_shift
  %ci_card_table_address = load i64, ptr @ci_card_table_address
  %card_base_addr = inttoptr i64 %ci_card_table_address to ptr
  %card_adr = getelementptr inbounds i8, ptr %card_base_addr, i64 %card_offset
  %oop.int = ptrtoint ptr addrspace(1) %oop to i64
  %xor_val = xor i64 %addr.int, %oop.int
  %hr_grain_bytes = load i64, ptr @HeapRegion.LogOfHRGrainBytes
  %xor_res = lshr i64 %xor_val, %hr_grain_bytes
  %is_zero = icmp eq i64 %xor_res, 0
  br i1 %is_zero, label %post_barrier_done, label %same_region_filtered

post_barrier_done:
  ret void

same_region_filtered:
  %is_null = icmp eq ptr addrspace(1) %oop, null
  br i1 %is_null, label %post_barrier_done, label %val_nullptr_filtered

val_nullptr_filtered:
  %card_value = load atomic i8, ptr %card_adr unordered, align 1
  %young_card = load i8, ptr @G1CardTable.g1_young_card_val
  %is_young = icmp eq i8 %card_value, %young_card
  br i1 %is_young, label %post_barrier_done, label %young_card_filtered

young_card_filtered:
  ; TODO: fence seq_cst is overly strict here. We only need StoreStore
  ; ordering between the oop store and the card table read/write
  fence seq_cst
  %card_val_reload = load atomic i8, ptr %card_adr unordered, align 1
  %dirty_card = load i8, ptr @G1CardTable.dirty_card_val
  %is_dirty = icmp eq i8 %card_val_reload, %dirty_card
  br i1 %is_dirty, label %post_barrier_done, label %store_dirty_block

store_dirty_block:
  store atomic i8 0, ptr %card_adr release, align 1
  %index = load i64, ptr addrspace(2) %index_adr
  %is_full = icmp eq i64 %index, 0
  br i1 %is_full, label %buffer_is_full, label %store_in_buffer

buffer_is_full:
  %callee_addr = load i64, ptr @G1BarrierSetRuntime.write_ref_field_post_entry
  %callee = inttoptr i64 %callee_addr to ptr
  %current_thread = call hotspotcc ptr @jeandle.current_thread()
  call void %callee(ptr %card_adr, ptr %current_thread) #0
  br label %post_barrier_done

store_in_buffer:
  %wordsize = load i64, ptr @WordSize
  %next_index = sub i64 %index, %wordsize
  %buffer = load ptr, ptr addrspace(2) %buffer_adr
  %log_addr = getelementptr inbounds i8, ptr %buffer, i64 %next_index
  store atomic ptr %card_adr, ptr %log_addr unordered, align 8
  store atomic i64 %next_index, ptr addrspace(2) %index_adr unordered, align 8
  br label %post_barrier_done
}

; Declaration of Java pre barrier.
declare hotspotcc void @jeandle.pre_barrier(ptr addrspace(1) %addr)

; Declaration of Java post barrier.
declare hotspotcc void @jeandle.post_barrier(ptr addrspace(1) %addr, ptr addrspace(1) nocapture %oop)

; Identity marker for Java type narrowing.
; Semantically returns the same oop. The java-klass return attribute is attached
; at call sites, not here.
define hotspotcc ptr addrspace(1) @jeandle.assume_java_type(ptr addrspace(1) %oop) noinline "lower-phase"="2" #0 {
entry:
  ret ptr addrspace(1) %oop
}

; Implementation of Java checkcast operation
define hotspotcc i1 @jeandle.checkcast(ptr addrspace(0) nocapture %super_klass, ptr addrspace(1) nocapture %oop) noinline "lower-phase"="0" #0 {
entry:
  %is_null = icmp eq ptr addrspace(1) %oop, null
  br i1 %is_null, label %return_true, label %check_subtype

return_true:
  ret i1 true

check_subtype:
  %is_subtype = call hotspotcc i1 @jeandle.check_instanceof(ptr addrspace(0) %super_klass, ptr addrspace(1) nocapture nonnull %oop)

  ret i1 %is_subtype
}

; Implementation of array store check operation
; lower-phase=2: survive JavaOperationLower(0) so the call reaches both PEA
; and the post-PEA TypeCheckElimination pass. PEA folds checks on virtual arrays;
; TypeCheckElimination also folds compatible stores to exact concrete arrays
; when PEA is disabled. Expanding it earlier means
; exposing a raw load of the array's klass header; resolveAccess returns
; nullopt for header offsets and processLoad marks the array ineligible —
; defeating array virtualization for EVERY aastore into an Object[] (the
; frontend emits this check for every reference array store).
define hotspotcc i1 @jeandle.array_store_check(ptr addrspace(1) nocapture %oop, ptr addrspace(1) nocapture %array_oop) noinline "lower-phase"="2" #0 {
entry:
  %is_null = icmp eq ptr addrspace(1) %oop, null
  br i1 %is_null, label %return_true, label %check_subtype

return_true:
  ret i1 true

check_subtype:
  %array_klass = call hotspotcc ptr addrspace(0) @jeandle.load_klass(ptr addrspace(1) %array_oop)
  %element_klass = call hotspotcc ptr addrspace(0) @jeandle.load_array_element_klass(ptr addrspace(0) %array_klass)
  %is_subtype = call hotspotcc i1 @jeandle.check_instanceof(ptr addrspace(0) %element_klass, ptr addrspace(1) nocapture nonnull %oop)

  ret i1 %is_subtype
}

; Implementation of Java idiv operation
define hotspotcc i32 @jeandle.idiv(i32 %dividend, i32 %divisor) noinline "lower-phase"="0" #0 {
entry:
  ; Check if dividend == Integer.MIN_VALUE (-2147483648)
  %is_min_int = icmp eq i32 %dividend, -2147483648
  br i1 %is_min_int, label %check_divisor, label %normal_idiv

check_divisor:
  %is_minus_one = icmp eq i32 %divisor, -1
  br i1 %is_minus_one, label %return_min_int, label %normal_idiv

return_min_int:
  ret i32 -2147483648

normal_idiv:
  %result = sdiv i32 %dividend, %divisor
  ret i32 %result
}

; Implementation of Java irem operation
define hotspotcc i32 @jeandle.irem(i32 %dividend, i32 %divisor) noinline "lower-phase"="0" #0 {
entry:
  ; Check if dividend == Integer.MIN_VALUE (-2147483648)
  %is_min_int = icmp eq i32 %dividend, -2147483648
  br i1 %is_min_int, label %check_divisor, label %normal_irem

check_divisor:
  %is_minus_one = icmp eq i32 %divisor, -1
  br i1 %is_minus_one, label %return_zero, label %normal_irem

return_zero:
  ret i32 0

normal_irem:
  %result = srem i32 %dividend, %divisor
  ret i32 %result
}

; Implementation of Java ldiv operation
define hotspotcc i64 @jeandle.ldiv(i64 %dividend, i64 %divisor) noinline "lower-phase"="0" #0 {
entry:
  ; Check if dividend == Long.MIN_VALUE (-9223372036854775808)
  %is_min_long = icmp eq i64 %dividend, -9223372036854775808
  br i1 %is_min_long, label %check_divisor, label %normal_ldiv

check_divisor:
  %is_minus_one = icmp eq i64 %divisor, -1
  br i1 %is_minus_one, label %return_min_long, label %normal_ldiv

return_min_long:
  ret i64 -9223372036854775808

normal_ldiv:
  %result = sdiv i64 %dividend, %divisor
  ret i64 %result
}

; Implementation of Java lrem operation
define hotspotcc i64 @jeandle.lrem(i64 %dividend, i64 %divisor) noinline "lower-phase"="0" #0 {
entry:
  ; Check if dividend == Long.MIN_VALUE (-9223372036854775808)
  %is_min_long = icmp eq i64 %dividend, -9223372036854775808
  br i1 %is_min_long, label %check_divisor, label %normal_lrem

check_divisor:
  %is_minus_one = icmp eq i64 %divisor, -1
  br i1 %is_minus_one, label %return_zero, label %normal_lrem

return_zero:
  ret i64 0

normal_lrem:
  %result = srem i64 %dividend, %divisor
  ret i64 %result
}

; Check if the object is value based
; lower-phase=2: survive JavaOperationLower(0) so the call reaches PEA, which
; folds it via foldCheckIfValueBased (collapsing it to a constant when the
; receiver's exact klass is known — always the case for a virtual object).
; At lower-phase=0 the body's raw klass-header load marks the receiver
; ineligible, defeating monitor elision for every value-based-candidate
; receiver.
define hotspotcc i1 @jeandle.check_if_value_based(ptr addrspace(1) nocapture %obj) "lower-phase"="2" #0 {
entry:
  %obj_klass = call hotspotcc ptr addrspace(0) @jeandle.load_klass(ptr addrspace(1) %obj)
  %access_flags_offset = load i32, ptr @Klass.access_flags_offset
  %access_flags_addr = getelementptr inbounds i8, ptr addrspace(0) %obj_klass, i32 %access_flags_offset
  %access_flags = load i32, ptr addrspace(0) %access_flags_addr
  %is_value_based_mask = load i32, ptr @JVM_ACC_IS_VALUE_BASED_CLASS
  %masked_value = and i32 %access_flags, %is_value_based_mask
  %is_value_based = icmp ne i32 %masked_value, 0
  ret i1 %is_value_based
}

; Register finalizer for an object only when its exact klass requires it.
; lower-phase=2: survive JavaOperationLower(0) so the call reaches PEA, which
; folds it via foldRegisterFinalizerIfNeeded (eliding it for non-finalizer
; klasses). At lower-phase=0 the body is expanded before PEA, exposing a raw
; load of the object's klass header; resolveAccess returns nullopt for header
; offsets and processLoad markIneligible's the object — defeating virtualization
; for EVERY allocation (every alloc has this finalizer check).
define hotspotcc void @jeandle.register_finalizer_if_needed(ptr addrspace(1) %obj) noinline "lower-phase"="2" {
entry:
  %obj_klass = call hotspotcc ptr addrspace(0) @jeandle.load_klass(ptr addrspace(1) %obj)
  %access_flags_offset = load i32, ptr @Klass.access_flags_offset
  %access_flags_addr = getelementptr inbounds i8, ptr addrspace(0) %obj_klass, i32 %access_flags_offset
  %access_flags = load i32, ptr addrspace(0) %access_flags_addr
  %has_finalizer_mask = load i32, ptr @JVM_ACC_HAS_FINALIZER
  %masked_value = and i32 %access_flags, %has_finalizer_mask
  %has_finalizer = icmp ne i32 %masked_value, 0
  br i1 %has_finalizer, label %register_finalizer, label %return

register_finalizer:
  %current_thread = call hotspotcc ptr @jeandle.current_thread()
  call hotspotcc void @SharedRuntime_register_finalizer(ptr %current_thread, ptr addrspace(1) %obj)
  br label %return

return:
  ret void
}

; Check if the lock is inflated
define hotspotcc i1 @jeandle.check_inflated(i64 %mark_word) "lower-phase"="0" #0 {
entry:
  %markWord_monitor_value = load i64, ptr @markWord.monitor_value
  %masked_value = and i64 %mark_word, %markWord_monitor_value
  %is_inflated = icmp ne i64 %masked_value, 0
  ret i1 %is_inflated
}

; Try to acquire the monitor lock when the lock is inflated
define hotspotcc i1 @jeandle.try_acquire_monitor_lock(i64 %mark_word, ptr addrspace(0) nocapture %lock) "lower-phase"="0" #0 {
entry:
  %monitor_ptr = inttoptr i64 %mark_word to ptr
  %owner_offset_no_monitor_value = load i32, ptr @ObjectMonitor.owner_offset_no_monitor_value
  %owner_addr = getelementptr inbounds i8, ptr %monitor_ptr, i32 %owner_offset_no_monitor_value
  %current_thread = call hotspotcc ptr @jeandle.current_thread()
  %current_thread_as_int = ptrtoint ptr %current_thread to i64
  %monitor_cas = cmpxchg ptr %owner_addr, i64 0, i64 %current_thread_as_int acq_rel monotonic, align 8
  %displaced_header_offset = load i32, ptr @BasicLock.displaced_header_offset_in_bytes
  %destory_lock_record_addr = getelementptr inbounds i8, ptr %lock, i32 %displaced_header_offset
  %unused_mark_value = load i64, ptr @markWord.unused_mark_value
  store i64 %unused_mark_value, ptr %destory_lock_record_addr, align 8
  %monitor_acquied = extractvalue { i64, i1 } %monitor_cas, 1
  br i1 %monitor_acquied, label %return_true, label %check_recursive_monitor

check_recursive_monitor:
  %monitor_owner = extractvalue { i64, i1 } %monitor_cas, 0
  %is_recursive_monitor_lock = icmp eq i64 %monitor_owner, %current_thread_as_int
  br i1 %is_recursive_monitor_lock, label %increase_recursions, label %return_false

increase_recursions:
  %recursions_offset_no_monitor_value = load i32, ptr @ObjectMonitor.recursions_offset_no_monitor_value
  %recursions_addr = getelementptr inbounds i8, ptr %monitor_ptr, i32 %recursions_offset_no_monitor_value
  %recursions = load i64, ptr %recursions_addr, align 8
  %new_recursions = add i64 %recursions, 1
  store i64 %new_recursions, ptr %recursions_addr, align 8
  br label %return_true

return_true:
  ret i1 true

return_false:
  ret i1 false
}

; Increment held_monitor_count in JavaThread
define hotspotcc void @jeandle.increment_lock_count() "lower-phase"="0" #0 {
entry:
  %held_monitor_count_offset = load i32, ptr @JavaThread.held_monitor_count_offset
  %held_monitor_count_offset_zext = zext i32 %held_monitor_count_offset to i64
  %held_monitor_count_addr = inttoptr i64 %held_monitor_count_offset_zext to ptr addrspace(2)
  %held_monitor_count = load i64, ptr addrspace(2) %held_monitor_count_addr, align 8
  %new_held_monitor_count = add i64 %held_monitor_count, 1
  store i64 %new_held_monitor_count, ptr addrspace(2) %held_monitor_count_addr, align 8
  ret void
}

; Decrement held_monitor_count in JavaThread
define hotspotcc void @jeandle.decrement_lock_count() "lower-phase"="0" #0 {
entry:
  %held_monitor_count_offset = load i32, ptr @JavaThread.held_monitor_count_offset
  %held_monitor_count_offset_zext = zext i32 %held_monitor_count_offset to i64
  %held_monitor_count_addr = inttoptr i64 %held_monitor_count_offset_zext to ptr addrspace(2)
  %held_monitor_count = load i64, ptr addrspace(2) %held_monitor_count_addr, align 8
  %new_held_monitor_count = sub i64 %held_monitor_count, 1
  store i64 %new_held_monitor_count, ptr addrspace(2) %held_monitor_count_addr, align 8
  ret void
}

; clear the lock stack top oop in debug mode
define hotspotcc void @jeandle.clear_oop_in_lock_stack_top(i32 %lock_stack_top) "lower-phase"="0" #0 {
entry:
  %is_debug = load i1, ptr @DEBUG_MODE
  br i1 %is_debug, label %debug_path, label %release_path

debug_path:
  %lock_stack_top_zext = zext i32 %lock_stack_top to i64
  %clear_oop_addr = inttoptr i64 %lock_stack_top_zext to ptr addrspace(2)
  store atomic i64 0, ptr addrspace(2) %clear_oop_addr unordered, align 8
  ret void

release_path:
  ret void
}

; Monitor enter/exit JavaOps are lower-phase=2 so they survive
; JavaOperationLower(0) (which runs before PEA) and reach PEA as a single
; opaque call each. PEA then folds balanced monitorenter/monitorexit pairs on
; virtual receivers (foldMonitorEnter/foldMonitorExit) and, on a deopt,
; records the lock as eliminated (deopt descriptor MonitorType index=1 with a
; VORef owner), which HotSpot re-acquires via relock_objects. At lower-phase=0
; the body is inlined before PEA into raw mark-word cmpxchg memory ops that
; PEA cannot recognize, so no lock would ever be elided. (Same lever already
; used by register_finalizer_if_needed and the allocation intrinsics.)
;
; Implementation of monitorenter when LockingMode == 0. A complete JavaOp:
; the fast path attempts the lock; every failure path falls through to the
; slow path, which delegates to the SharedRuntime slow routine. The fast path
; increments held_monitor_count on success; the slow path does NOT (the runtime
; routine manages its own counter), preserving the pre-refactor semantics.
; Fast path implementation of monitorenter when LockingMode == 0
define hotspotcc void @jeandle.monitorenter_with_monitor_lock(ptr addrspace(1) nocapture %obj, ptr addrspace(0) nocapture %lock) "lower-phase"="2" #0 {
entry:
  %mark_offset = load i32, ptr @oopDesc.mark_offset_in_bytes
  %mark_word_addr = getelementptr inbounds i8, ptr addrspace(1) %obj, i32 %mark_offset
  %mark_word = load atomic i64, ptr addrspace(1) %mark_word_addr unordered, align 8
  %is_inflated = call hotspotcc i1 @jeandle.check_inflated(i64 %mark_word)
  br i1 %is_inflated, label %monitor_lock_fast_path, label %slow_path

monitor_lock_fast_path:
  %acquired = call hotspotcc i1 @jeandle.try_acquire_monitor_lock(i64 %mark_word, ptr addrspace(0) %lock)
  br i1 %acquired, label %increment_lock_count_and_return, label %slow_path

increment_lock_count_and_return:
  call hotspotcc void @jeandle.increment_lock_count()
  ret void

slow_path:
  %current_thread = call hotspotcc ptr @jeandle.current_thread()
  call hotspotcc void @SharedRuntime_complete_monitor_locking_C(ptr addrspace(1) %obj, ptr addrspace(0) %lock, ptr %current_thread)
  ret void
}

; Implementation of monitorenter when LockingMode == 1. Complete JavaOp:
; thin-lock fast path (with recursive-owner check); all failures go to slow_path.
; Fast path implementation of monitorenter when LockingMode == 1
define hotspotcc void @jeandle.monitorenter_with_thin_lock(ptr addrspace(1) nocapture %obj, ptr addrspace(0) nocapture %lock) "lower-phase"="2" #0 {
entry:
  %mark_offset = load i32, ptr @oopDesc.mark_offset_in_bytes
  %mark_word_addr = getelementptr inbounds i8, ptr addrspace(1) %obj, i32 %mark_offset
  %mark_word = load atomic i64, ptr addrspace(1) %mark_word_addr unordered, align 8
  %is_inflated = call hotspotcc i1 @jeandle.check_inflated(i64 %mark_word)
  br i1 %is_inflated, label %monitor_lock_fast_path, label %thin_lock_path

thin_lock_path:
  %markWord_unlocked_value = load i64, ptr @markWord.unlocked_value
  %unlocked_mark_word = or i64 %mark_word, %markWord_unlocked_value
  store i64 %unlocked_mark_word, ptr %lock, align 8
  %lock_as_int = ptrtoint ptr %lock to i64
  %thin_lock_cas = cmpxchg ptr addrspace(1) %mark_word_addr, i64 %unlocked_mark_word, i64 %lock_as_int acq_rel monotonic, align 8
  %thin_lock_acquired = extractvalue { i64, i1 } %thin_lock_cas, 1
  br i1 %thin_lock_acquired, label %increment_lock_count_and_return, label %check_recursive_thin_lock

check_recursive_thin_lock:
  %stack_top = call hotspotcc i64 @jeandle.get_stack_pointer()
  %thin_lock_owner = extractvalue { i64, i1 } %thin_lock_cas, 0
  %offset_from_sp = sub i64 %thin_lock_owner, %stack_top
  %check_recursive_mask_value = load i64, ptr @check_recursive_mask_value
  %recursive_masked_value = and i64 %offset_from_sp, %check_recursive_mask_value
  store i64 %recursive_masked_value, ptr %lock, align 8
  %is_recursive_thin_lock = icmp eq i64 %recursive_masked_value, 0
  br i1 %is_recursive_thin_lock, label %increment_lock_count_and_return, label %slow_path

monitor_lock_fast_path:
  %acquired = call hotspotcc i1 @jeandle.try_acquire_monitor_lock(i64 %mark_word, ptr addrspace(0) %lock)
  br i1 %acquired, label %increment_lock_count_and_return, label %slow_path

increment_lock_count_and_return:
  call hotspotcc void @jeandle.increment_lock_count()
  ret void

slow_path:
  %current_thread = call hotspotcc ptr @jeandle.current_thread()
  call hotspotcc void @SharedRuntime_complete_monitor_locking_C(ptr addrspace(1) %obj, ptr addrspace(0) %lock, ptr %current_thread)
  ret void
}

; Implementation of monitorenter when LockingMode == 2. Complete JavaOp:
; lightweight-lock fast path (CAS + lock-stack push); all failures (stack full,
; CAS lost, inflated-monitor acquire fail) go to slow_path.
; Fast path implementation of monitorenter when LockingMode == 2
define hotspotcc void @jeandle.monitorenter_with_lightweight_lock(ptr addrspace(1) nocapture %obj, ptr addrspace(0) nocapture %lock) "lower-phase"="2" #0 {
entry:
  %mark_offset = load i32, ptr @oopDesc.mark_offset_in_bytes
  %mark_word_addr = getelementptr inbounds i8, ptr addrspace(1) %obj, i32 %mark_offset
  %mark_word = load atomic i64, ptr addrspace(1) %mark_word_addr unordered, align 8
  %is_inflated = call hotspotcc i1 @jeandle.check_inflated(i64 %mark_word)
  br i1 %is_inflated, label %monitor_lock_fast_path, label %lightweight_lock_path

lightweight_lock_path:
  %lock_stack_top_offset = load i32, ptr @JavaThread.lock_stack_top_offset
  %lock_stack_top_offset_zext = zext i32 %lock_stack_top_offset to i64
  %lock_stack_top_addr = inttoptr i64 %lock_stack_top_offset_zext to ptr addrspace(2)
  %lock_stack_top = load i32, ptr addrspace(2) %lock_stack_top_addr, align 4
  %lock_stack_end = load i32, ptr @JavaThread.lock_stack_end
  %is_lock_stack_full = icmp sge i32 %lock_stack_top, %lock_stack_end
  br i1 %is_lock_stack_full, label %slow_path, label %lightweight_lock

lightweight_lock:
  %markWord_clear_lock_mask = load i64, ptr @markWord.clear_lock_mask
  %mark_word_clear_lock = and i64 %mark_word, %markWord_clear_lock_mask
  %markWord_unlocked_value = load i64, ptr @markWord.unlocked_value
  %unlocked_mark_word = or i64 %mark_word_clear_lock, %markWord_unlocked_value
  %lightweight_lock_cas = cmpxchg ptr addrspace(1) %mark_word_addr, i64 %unlocked_mark_word, i64 %mark_word_clear_lock acq_rel monotonic, align 8
  %lightweight_lock_acquired = extractvalue { i64, i1 } %lightweight_lock_cas, 1
  br i1 %lightweight_lock_acquired, label %push_oop_to_lock_stack, label %slow_path

push_oop_to_lock_stack:
  %lock_stack_top_zext = zext i32 %lock_stack_top to i64
  %store_oop_addr = inttoptr i64 %lock_stack_top_zext to ptr addrspace(2)
  store atomic ptr addrspace(1) %obj, ptr addrspace(2) %store_oop_addr unordered, align 8
  %oopSize = load i32, ptr @oopSize
  %lock_stack_top_increased = add i32 %lock_stack_top, %oopSize
  store i32 %lock_stack_top_increased, ptr addrspace(2) %lock_stack_top_addr, align 4
  br label %increment_lock_count_and_return

monitor_lock_fast_path:
  %acquired = call hotspotcc i1 @jeandle.try_acquire_monitor_lock(i64 %mark_word, ptr addrspace(0) %lock)
  br i1 %acquired, label %increment_lock_count_and_return, label %slow_path

increment_lock_count_and_return:
  call hotspotcc void @jeandle.increment_lock_count()
  ret void

slow_path:
  %current_thread = call hotspotcc ptr @jeandle.current_thread()
  call hotspotcc void @SharedRuntime_complete_monitor_locking_C(ptr addrspace(1) %obj, ptr addrspace(0) %lock, ptr %current_thread)
  ret void
}

; Implementation of monitorexit when LockingMode == 0. Complete JavaOp: the
; fast path releases the lock and decrements held_monitor_count on success;
; failure falls through to the slow path. The slow path does NOT call
; decrement_lock_count (the pre-refactor user-IR slow path did not either);
; the runtime routine handles release on its own.
; Fast path implementation of monitorexit when LockingMode == 0
define hotspotcc void @jeandle.monitorexit_with_monitor_lock(ptr addrspace(1) nocapture %obj, ptr addrspace(0) nocapture %lock) "lower-phase"="2" #0 {
entry:
  %mark_offset = load i32, ptr @oopDesc.mark_offset_in_bytes
  %mark_word_addr = getelementptr inbounds i8, ptr addrspace(1) %obj, i32 %mark_offset
  %mark_word = load atomic i64, ptr addrspace(1) %mark_word_addr unordered, align 8
  %released = call hotspotcc i1 @jeandle.try_release_monitor_lock(i64 %mark_word)
  br i1 %released, label %decrement_lock_count_and_return, label %slow_path

decrement_lock_count_and_return:
  call hotspotcc void @jeandle.decrement_lock_count()
  ret void

slow_path:
  %current_thread = call hotspotcc ptr @jeandle.current_thread()
  %callee_addr = load i64, ptr @SharedRuntime.complete_monitor_unlocking_C
  %callee = inttoptr i64 %callee_addr to ptr
  call void %callee(ptr addrspace(1) %obj, ptr addrspace(0) %lock, ptr %current_thread) #0
  ret void
}

; Implementation of monitorexit when LockingMode == 1. Complete JavaOp: thin
; recursive-unlock fast path, else inflated release, else thin-unlock CAS;
; all failures fall through to slow_path.
; Fast path implementation of monitorexit when LockingMode == 1
define hotspotcc void @jeandle.monitorexit_with_thin_lock(ptr addrspace(1) nocapture %obj, ptr addrspace(0) nocapture %lock) "lower-phase"="2" #0 {
entry:
  %displaced_header_offset = load i32, ptr @BasicLock.displaced_header_offset_in_bytes
  %displaced_header_addr = getelementptr inbounds i8, ptr %lock, i32 %displaced_header_offset
  %displaced_header = load i64, ptr %displaced_header_addr, align 8
  %is_recursive_stack_unlock = icmp eq i64 %displaced_header, 0
  br i1 %is_recursive_stack_unlock, label %decrement_lock_count_and_return, label %check_if_lock_is_inflated

check_if_lock_is_inflated:
  %mark_offset = load i32, ptr @oopDesc.mark_offset_in_bytes
  %mark_word_addr = getelementptr inbounds i8, ptr addrspace(1) %obj, i32 %mark_offset
  %mark_word = load atomic i64, ptr addrspace(1) %mark_word_addr unordered, align 8
  %is_inflated = call hotspotcc i1 @jeandle.check_inflated(i64 %mark_word)
  br i1 %is_inflated, label %monitor_unlock_fast_path, label %thin_unlock_path

thin_unlock_path:
  %lock_as_int = ptrtoint ptr %lock to i64
  %thin_lock_cas = cmpxchg ptr addrspace(1) %mark_word_addr, i64 %lock_as_int, i64 %displaced_header acq_rel monotonic, align 8
  %thin_lock_released = extractvalue { i64, i1 } %thin_lock_cas, 1
  br i1 %thin_lock_released, label %decrement_lock_count_and_return, label %slow_path

monitor_unlock_fast_path:
  %released = call hotspotcc i1 @jeandle.try_release_monitor_lock(i64 %mark_word)
  br i1 %released, label %decrement_lock_count_and_return, label %slow_path

decrement_lock_count_and_return:
  call hotspotcc void @jeandle.decrement_lock_count()
  ret void

slow_path:
  %current_thread = call hotspotcc ptr @jeandle.current_thread()
  %callee_addr = load i64, ptr @SharedRuntime.complete_monitor_unlocking_C
  %callee = inttoptr i64 %callee_addr to ptr
  call void %callee(ptr addrspace(1) %obj, ptr addrspace(0) %lock, ptr %current_thread) #0
  ret void
}

; Implementation of monitorexit when LockingMode == 2. Complete JavaOp:
; lightweight-unlock fast path (CAS + lock-stack pop); inflated release with an
; anonymous-owner guard. All failures fall through to slow_path.
; Fast path implementation of monitorexit when LockingMode == 2
define hotspotcc void @jeandle.monitorexit_with_lightweight_lock(ptr addrspace(1) nocapture %obj, ptr addrspace(0) nocapture %lock) "lower-phase"="2" #0 {
entry:
  %mark_offset = load i32, ptr @oopDesc.mark_offset_in_bytes
  %mark_word_addr = getelementptr inbounds i8, ptr addrspace(1) %obj, i32 %mark_offset
  %mark_word = load atomic i64, ptr addrspace(1) %mark_word_addr unordered, align 8
  %is_inflated = call hotspotcc i1 @jeandle.check_inflated(i64 %mark_word)
  br i1 %is_inflated, label %check_anonymous_owner, label %lightweight_unlock_path

lightweight_unlock_path:
  %markWord_unlocked_value = load i64, ptr @markWord.unlocked_value
  %unlocked_mark_word = or i64 %mark_word, %markWord_unlocked_value
  %lightweight_lock_cas = cmpxchg ptr addrspace(1) %mark_word_addr, i64 %mark_word, i64 %unlocked_mark_word acq_rel monotonic, align 8
  %lightweight_lock_released = extractvalue { i64, i1 } %lightweight_lock_cas, 1
  br i1 %lightweight_lock_released, label %pop_oop_from_lock_stack, label %slow_path

pop_oop_from_lock_stack:
  %lock_stack_top_offset = load i32, ptr @JavaThread.lock_stack_top_offset
  %lock_stack_top_offset_zext = zext i32 %lock_stack_top_offset to i64
  %lock_stack_top_addr = inttoptr i64 %lock_stack_top_offset_zext to ptr addrspace(2)
  %lock_stack_top = load i32, ptr addrspace(2) %lock_stack_top_addr, align 4
  %oopSize = load i32, ptr @oopSize
  %new_lock_stack_top = sub i32 %lock_stack_top, %oopSize
  store i32 %new_lock_stack_top, ptr addrspace(2) %lock_stack_top_addr, align 4
  call hotspotcc void @jeandle.clear_oop_in_lock_stack_top(i32 %new_lock_stack_top)
  br label %decrement_lock_count_and_return

check_anonymous_owner:
  %monitor_ptr = inttoptr i64 %mark_word to ptr
  %owner_offset_no_monitor_value = load i32, ptr @ObjectMonitor.owner_offset_no_monitor_value
  %owner_addr = getelementptr inbounds i8, ptr %monitor_ptr, i32 %owner_offset_no_monitor_value
  %owner = load atomic volatile i64, ptr %owner_addr unordered, align 8
  %anonymous_owner_mask = load i64, ptr @ObjectMonitor.ANONYMOUS_OWNER
  %masked_owner = and i64 %owner, %anonymous_owner_mask
  %is_anonymous_owner = icmp ne i64 %masked_owner, 0
  br i1 %is_anonymous_owner, label %slow_path, label %monitor_unlock_fast_path

monitor_unlock_fast_path:
  %released = call hotspotcc i1 @jeandle.try_release_monitor_lock(i64 %mark_word)
  br i1 %released, label %decrement_lock_count_and_return, label %slow_path

decrement_lock_count_and_return:
  call hotspotcc void @jeandle.decrement_lock_count()
  ret void

slow_path:
  %current_thread = call hotspotcc ptr @jeandle.current_thread()
  %callee_addr = load i64, ptr @SharedRuntime.complete_monitor_unlocking_C
  %callee = inttoptr i64 %callee_addr to ptr
  call void %callee(ptr addrspace(1) %obj, ptr addrspace(0) %lock, ptr %current_thread) #0
  ret void
}

attributes #0 = { nounwind "gc-leaf-function" }
attributes #1 = { noinline mustprogress nounwind willreturn memory(read) "gc-leaf-function" }
